package point;

import com.heigo.model.PointDomain;

// 过生日，则加10分，并且将当月交易笔数翻倍后再计算积分
rule "birthdayPoint"
    salience 100    //优先级，数值越大越先执行，这个可以控制规则的执行顺序。
    lock-on-active true //通过这个标签，可以控制当前的规则只会被执行一次
    when
        $pointDomain:PointDomain(birthDay==true)
    then
        $pointDomain.setPoint($pointDomain.getPoint()+10);  //加10积分
        $pointDomain.setBuyNums($pointDomain.getBackNums()*2);
        $pointDomain.setBuyMoney($pointDomain.getBuyMoney()*2);
        $pointDomain.setBillThisMonth($pointDomain.getBillThisMonth()*2);       //以上三个翻倍

        $pointDomain.recordPointLog($pointDomain.getUserName(),"birthdayPoint");
end

//2011-01-08 - 2011-08-08每月信用卡还款3次以上，每满3笔赠送30分
rule "billThisMonthPoint"
    salience 99
    lock-on-active true
    date-effective "2011-01-08 23:59:59"     //设置规则的生效时间，时间格式同上
    date-expires "2011-08-08 23:59:59"       //设置规则的过期时间
    when
        $pointDomain:PointDomain(billThisMonth>=3)
    then
        $pointDomain.setPoint($pointDomain.getPoint()+$pointDomain.getBillThisMonth()/3*30);
        $pointDomain.recordPointLog($pointDomain.getUserName(),"billThisMonthPoint");
end

// 当月购物总金额100以上，每100元赠送10分
rule "buyMoneyPoint"
    salience 98
    lock-on-active true
    when
        pointDomain:PointDomain(buyMoney>=100)
    then
        pointDomain.setPoint(pointDomain.getPoint()+(int)pointDomain.getBuyMoney()/100*10);
        pointDomain.recordPointLog(pointDomain.getUserName(),"buyMoneyPoint");
end

//  当月购物次数5次以上，每五次赠送50分
rule "buyNumsPoint"
    salience 97
    lock-on-active true
    when
        $pointDomain:PointDomain(buyNums>=5)
    then
        $pointDomain.setPoint($pointDomain.getPoint()+$pointDomain.getBuyNums()/5*50);
        $pointDomain.recordPointLog($pointDomain.getUserName(),"buyNumsPoint");
end

// 特别的，如果全部满足了要求，则额奖励100积分
rule "allFitPoint"
    salience 96
    lock-on-active true
    when
        $pointDomain:PointDomain(buyNums>=5&& billThisMonth>=3 && buyMoney>=100)
    then
        $pointDomain.setPoint($pointDomain.getPoint()+100);
        $pointDomain.recordPointLog($pointDomain.getUserName(),"allFitPoint");
end